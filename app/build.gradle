apply plugin: 'com.android.application'
apply plugin: "org.sonarqube"

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId "org.andstatus.app"
        minSdkVersion 16
        targetSdkVersion 25
        versionName "35.00"
        versionCode 225

        testApplicationId "org.andstatus.app.tests"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // To test arguments:
        // testInstrumentationRunnerArgument "executionMode", "travisTest"
        project.ext.set("archivesBaseName", "AndStatus-$versionName".toString());
        project.ext.set("versionName", "$versionName".toString());
    }

    buildTypes {
        release {
            minifyEnabled false
            lintOptions {
                warning 'MissingTranslation','InvalidPackage'
            }
        }
        debug {
            testCoverageEnabled true
        }
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    // This enables long timeouts required on slow environments, e.g. Travis
    adbOptions {
        timeOutInMs 20 * 60 * 1000  // 20 minutes
        installOptions "-d","-t"
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "AndStatus"
        property "sonar.projectKey", "andstatus"
        property "sonar.projectVersion", project.ext.get("versionName")

        property "sonar.sourceEncoding","UTF-8"
        property "sonar.sources","./src/main"
        property "sonar.tests","./src/androidTest"
        // See http://docs.sonarqube.org/display/SONAR/Narrowing+the+Focus
        property "sonar.exclusions","build/**,libs/**,**/*.png,**/*.json,**/*.iml"

        property "sonar.import_unknown_files", true

        property "sonar.android.lint.report", "./build/outputs/lint-results.xml"
        property "sonar.jacoco.reportPath", fileTree(dir: project.projectDir, includes: ['**/*.exec'])
        property "sonar.jacoco.itReportPath", fileTree(dir: project.projectDir, includes: ['**/*.ec'])
    }
}

dependencies {
    compile 'com.github.stephenc.jcip:jcip-annotations:1.0-1'
    // http://stephenc.github.io/jcip-annotations/dependency-info.html
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'ch.acra:acra:4.9.2'  // https://github.com/ACRA/acra/wiki/AdvancedUsage
    compile 'cz.msebera.android:httpclient:4.4.1.2'
    // https://github.com/smarek/httpclient-android
    /* Don't use these dependencies for now, because AIDE doesn't support exclusion
                   of transitive dependencies.
                compile 'org.apache.httpcomponents:httpmime:4.3.5'
                Use the files from 'libs' folder instead:	*/
    compile files('libs/httpmime-4.3.5.jar')
    compile files('libs/signpost-httpclient-android-1.2.1.2.jar')
    compile files('libs/signpost-core-1.2.1.2.jar')
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.github.woxthebox:draglistview:1.3' // https://github.com/woxblom/DragListView
    compile 'com.github.scribejava:scribejava-core:4.1.0'
    compile 'junit:junit:4.12'

    androidTestCompile 'com.android.support.test:runner:0.6-alpha'
    androidTestCompile 'com.android.support.test:rules:0.6-alpha'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
}